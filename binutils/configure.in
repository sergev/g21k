dnl Process this file with autoconf to produce a configure script.
dnl $Id$
dnl
dnl copyright 1999 Egil Kvaleberg, egil@kvaleberg.no
dnl the GNU General Public License applies
dnl
dnl $Log: configure.in,v $
dnl
AC_INIT(config.def.in)

AM_INIT_AUTOMAKE(g21k_binutils, PL3)
AM_CONFIG_HEADER(config.h)
dnl Only most recent year required:
dnl COPYRIGHT="Copyright 1999 Egil Kvaleberg <egil@kvaleberg.no>"
BUGCATCHER="bug-g21@kvaleberg.no"

AC_REVISION(Configure for g21k-binutils $VERSION)
dnl We need brackets:
changequote(<<, >>)
VERDATE=`date | sed 's/^[^ ]* \([^ ]*\) *\([0-9]*\) .* \([0-9]*\)$/\2 \1 \3/'`
changequote([, ])
AC_SUBST(VERSION)
AC_SUBST(VERDATE)
AC_SUBST(PACKAGE)
AC_SUBST(COPYRIGHT)
AC_SUBST(BUGCATCHER)

dnl -----------------------------------------------------------------
dnl
dnl  Command line options
dnl
AC_ARG_WITH(adidsp, \
  [--with-adidsp=DIR      ADI_DSP default directory is DIR], \
	ADI_DSP="$withval")

if test x$ADI_DSP = x; then
	ADI_DSP=$ac_default_prefix
fi
AC_SUBST(ADI_DSP)

dnl -----------------------------------------------------------------
dnl
dnl  Check out the C compiler, tools and libraries
dnl

dnl Check for CC before anything else..
AC_PROG_CC

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AM_C_PROTOTYPES

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME

dnl Checks for CPU characteristics.
AC_C_BIGENDIAN
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(char *)

dnl Checks for compiler characteristics.
AC_C_CONST

AC_CACHE_CHECK([for packed struct], ac_cv_gnuc_packed,
[AC_TRY_COMPILE(,
changequote(<<, >>)dnl
<<
/* NEXTSTEP Gnu C does not support this, for instance */
struct { long a; short b; } __attribute__((packed)) s;
>>,
changequote([, ])dnl
ac_cv_gnuc_packed=yes, ac_cv_gnuc_packed=no)])
if test $ac_cv_gnuc_packed = yes; then
  AC_DEFINE(PACKED, [__attribute__((packed))])
else
  AC_DEFINE(PACKED, [/* packed */])
fi

dnl Checks for standard include file completeness.
AC_TYPE_SIGNAL
AC_TYPE_PID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_MODE_T

dnl Checks for library functions.
AC_FUNC_VPRINTF

dnl Definitions for AD tools

dnl Extra substitutions into config.h
AC_DEFINE_UNQUOTED(PACKAGE,   "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION,   "$VERSION")
AC_DEFINE_UNQUOTED(COPYRIGHT, "$COPYRIGHT")
AC_DEFINE_UNQUOTED(BUGCATCHER,"$BUGCATCHER")
AC_DEFINE_UNQUOTED(ADI_DSP,   "$ADI_DSP")

AC_OUTPUT(config.def g21k-binutils.spec, [ \
# Makefile uses this timestamp file to record whether config.h is up to date.
touch stamp-config])
